/* 
   NPL Gradle file

   https://github.com/jomifred/npl
   
*/

plugins {
  id "ca.coglinc.javacc" version "2.3.1"
  // see https://github.com/johnmartel/javaccPlugin
}

defaultTasks 'jar'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version '0.2-SNAPSHOT'
group   'org.jacamo'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    
    maven {
        url "http://jacamo.sourceforge.net/maven2"
    }
    
    /*flatDir {
	   dirs 'lib'
    }*/
}

configurations {
    umljavadoc
    // see http://www.umlgraph.org/doc/indexw.html#ClassCastException
}


dependencies {
    compile group: 'net.sf.jason', name: 'jason', version: '2.0.beta', changing: true
    testCompile group: 'junit', name: 'junit', version: '4.+'
    umljavadoc 'org.umlgraph:umlgraph:5.6'
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'src/examples'
        }
    }
}

jar {
    baseName 'npl'
    
    // exclude files from example
    exclude('*.class') 
    exclude('*.npl') 
}

clean {
    delete 'bin'
}


task example0 (type: JavaExec, dependsOn: classes) {
    main = 'Example0'
    classpath sourceSets.main.runtimeClasspath
}

task example1 (type: JavaExec, dependsOn: classes) {
    main = 'Example1'
    classpath sourceSets.main.runtimeClasspath
}

task example2 (type: JavaExec, dependsOn: classes) {
    main = 'Example2'
    classpath sourceSets.main.runtimeClasspath
}

task javadoc(overwrite: true, dependsOn: compileJava) {
    setDescription('Generates Javadoc API documentation with UMLGraph diagrams')
    setGroup(JavaBasePlugin.DOCUMENTATION_GROUP)

    doLast {
        ant.javadoc(classpath: (configurations.runtime + configurations.compile).asPath, 
                    sourcepath: file('src/main/java'), 
                    packagenames: '*',
                    destdir: "${docsDir}/../../doc/api",
                    private: 'true',
                    docletpath: configurations.umljavadoc.asPath) {
            doclet(name: 'org.umlgraph.doclet.UmlGraphDoc') {
                param(name: '-inferrel')
                param(name: '-inferdep')
                param(name: '-qualify')
                //param(name: '-T', value: 'svg')
                param(name: '-postfixpackage')
                param(name: '-hide', value: 'java.*')
                //param(name: '-collpackages', value: 'java.util.*')
                //param(name: '-nodefontsize', value: '9')
                //param(name: '-nodefontpackagesize', value: '7')
                //param(name: '-link', value: 'http://java.sun.com/j2se/1.5.0/docs/guide/javadoc/doclet/spec')
                //param(name: '-link', value: 'http://java.sun.com/j2se/1.5/docs/api')
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            
            artifactId 'npl'
        }
    }
}

compileJavacc {
    inputDirectory = file('src/main/javacc')
    outputDirectory = file(project.buildDir.absolutePath + '/generated/javacc/npl/parser')
}


jjdoc {
    outputDirectory = file(project.projectDir.absolutePath + '/doc')
    //arguments = [text: 'true']
}

// transfer local maven artifact to our maven2 repository at sourceforge
task publishMavenSF(dependsOn: [ 'build', 'publishToMavenLocal']) << {
    def wdir = System.getProperty("user.home")+'/.m2/repository/org/jacamo/npl'
    def tdir = 'jomifred,jacamo@web.sf.net:/home/project-web/jacamo/htdocs/maven2/org/jacamo/npl'
    exec {
        commandLine 'scp', '-r', project.version, tdir
        workingDir wdir
    }
    exec {
        commandLine 'cp', 'maven-metadata-local.xml', 'maven-metadata.xml'
        workingDir wdir
    }
    exec {
        commandLine 'scp', 'maven-metadata.xml', tdir
        workingDir wdir
    }
}

// Builds the distribution, documentation and a new release
task release(dependsOn: [ 'build', 'jjdoc', 'javadoc', 'fixTab', 'zipRel']) << {
} 

task fixTab {
    ant.fixcrlf(eol: 'lf',  includes: '**/*.txt,**/*.bat, **/*.adoc', srcdir: '.')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml', srcdir: 'src')
}

// copy files used to build a release
task copyRel << {
    copy {
        from '.'
        into 'build/npl-'+project.version
        include '*.adoc'
        include 'LICENSE'
        include 'scripts/**'
    }
    copy {
        from libsDir
        from configurations.runtime
        into 'build/npl-'+project.version+'/'+libsDirName
        include '**/*.jar'
    }
    copy {
        from 'doc'      
        into 'build/npl-'+project.version+'/doc' 
    }
    copy {
        from 'src/examples'      
        into 'build/npl-'+project.version+'/src/examples' 
    }
}

// creates a zip file for a release
task zipRel(type: Zip, dependsOn: copyRel) {
    from 'build/npl-'+project.version
    baseName 'npl'
}
